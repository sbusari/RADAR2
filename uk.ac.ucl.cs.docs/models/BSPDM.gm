Model BSPDM;

Objective Min ExpectedCostOfDisclosures = E(Cost_of_Disclosures);

Objective Min RiskOfCatastrophicDisclosures = P(Cost_of_Disclosures > 10^6);

Cost_of_Disclosures = 
    Nbr_HighConfidentialityLeaks * Cost_HighConfidentialityLeak
    + Nbr_MediumConfidentialityLeaks * Cost_MediumConfidentialityLeak
    + Nbr_LowConfidentialityLeaks * Cost_LowConfidentialityLeak;
    	
Nbr_HighConfidentialityLeaks = Nbr_LeakedDoc * Ratio_HighConfidentialityDocs;
Nbr_MediumConfidentialityLeaks = Nbr_LeakedDoc * Ratio_MediumConfidentialityDocs;
Nbr_LowConfidentialityLeaks = Nbr_LeakedDoc * Ratio_LowConfidentialityDocs;
   	
Ratio_HighConfidentialityDocs = parameter(uniform(0.5%, 2%));
Ratio_MediumConfidentialityDocs = parameter(uniform(4%, 10%));
Ratio_LowConfidentialityDocs = parameter(uniform(30%, 60%));

// other docs are not confidential 
    
Cost_HighConfidentialityLeak = parameter(normal_ci(0.5*10^6, 5*10^6));
Cost_MediumConfidentialityLeak = parameter(normal_ci(10^4, 5*10^4));
Cost_LowConfidentialityLeak = parameter(normal_ci(10^2, 10^4));

Nbr_LeakedDoc = Nbr_DocsLeakedByInsiders + Nbr_DocsStolenByOutsiders;

Nbr_DocsLeakedByInsiders = 
       Probability_InsidersLeak * 
       Nbr_DocsOnSharedDrive * 
       SharedDriveLeakRange;
       
Probability_InsidersLeak = parameter(10^-3);
SharedDriveLeakRange = parameter(triangular(10%, 50%, 100%));

Nbr_DocsStolenByOutsiders = Nbr_OfficeIntrusions* Nbr_DocsStolenPerIntrusion;
		
Nbr_OfficeIntrusions = decision("Building entry security"){
		"not guarded"	: parameter(triangular(0, 3, 6));
		"guarded" 	: parameter(triangular(0, 1, 2));
}

Nbr_DocsStolenPerIntrusion = Nbr_ExternalMediaStolenPerIntrusion * Nbr_DocsOnExternalMedia;
		
Nbr_ExternalMediaStolenPerIntrusion = Nbr_ExternalMediaInUse * Percentage_MediaStolenPerInstrusion;
		
Percentage_MediaStolenPerInstrusion = parameter(triangular(0, 1%, 10%));
/*Percentage_MediaStolenPerInstrusion = parameter(triangular(0, 0.01, 0.1));*/

Nbr_DocsOnSharedDrive = decision("Document Sharing Policy"){
    		"Neutral": parameter(143);
    		"Recommend Email": parameter(44);
    		"Recommend External Media": parameter(91);
} 

Nbr_DocsOnExternalMedia = decision("Document Sharing Policy"){
    		"Neutral": parameter(0);
    		"Recommend Email": parameter(0);
    		"Recommend External Media": parameter(52);
}

Nbr_ExternalMediaInUse = Nbr_DocsOnExternalMedia / Nbr_DocsPerMedia;

Nbr_DocsPerMedia = parameter(triangular(0, 5, 10)) ;
		           		